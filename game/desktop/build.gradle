plugins {
    id 'java'
    id 'java-library'
}

apply from: '../dependencies.gradle'

compileJava {
    sourceCompatibility JavaVersion.VERSION_1_8.toString()
    targetCompatibility JavaVersion.VERSION_1_8.toString()
}

group 'com.junglewar'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":game:core")

    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$project.gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-bullet-platform:$project.gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

project.ext.mainClassName = "com.xpeengine.demo.superjumper.DesktopMain"
project.ext.assetsDir = new File("../assets");

task run(dependsOn: classes, type: JavaExec) {
    group = 'other'
    description = 'Run JungleWar'

    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    workingDir = project.assetsDir
}

task dist(type: Jar, dependsOn: classes) {
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}